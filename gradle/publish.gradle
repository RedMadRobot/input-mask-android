apply plugin: 'com.github.dcendents.android-maven'

def publishPropertiesFile = rootProject.file("gradle/publish.properties")
def publishProperties = new Properties()

if (publishPropertiesFile.canRead()) {
    publishProperties.load(new FileInputStream(publishPropertiesFile))
}
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version publishProperties['lib_version']
group publishProperties['group_id']

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.kotlin.srcDirs
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/inputmask-release.aar")
            artifact androidSourcesJar
            groupId
            artifactId publishProperties['lib_name']
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = publishProperties['user']
    key = publishProperties['api_key']
    publications = ['Production']
    configurations = ['archives']
    override = true

    pkg {
        repo = publishProperties['repository']
        name = publishProperties['lib_name']
        description = publishProperties['lib_description']
        userOrg = publishProperties['organization_id']
        licenses = ['MIT']
        websiteUrl = publishProperties['lib_website']
        vcsUrl = publishProperties['lib_vcs']
        issueTrackerUrl = publishProperties['lib_issue_tracker']
        publish = true
        publicDownloadNumbers = true

        // set as true to test uploading
        dryRun = true

        version {
            name = this.version
            released = new Date()
            vcsTag = this.version
        }
    }
}

